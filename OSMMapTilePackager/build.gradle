description = "A tool to package OpenStreetMap tiles"
apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'application'
apply plugin: 'maven-publish'

group = project.property("pom.groupId")
version =  project.property("pom.version")
mainClassName = "org.osmdroid.mtp.OSMMapTilePackager"

test {
    testLogging {
        showStandardStreams = true
    }
}

java {
    withSourcesJar()
    withJavadocJar()

    sourceCompatibility(JavaVersion.VERSION_17)
    targetCompatibility(JavaVersion.VERSION_17)
}

tasks.withType(Javadoc).configureEach {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") { because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib") }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") { because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib") }
    }

    implementation("androidx.annotation:annotation:1.8.1")
    implementation 'org.apache.httpcomponents:httpmime:4.5.14'
    implementation 'org.apache.james:apache-mime4j:0.8.11'
    implementation "org.xerial:sqlite-jdbc:${project.property('xerial.sqlite.jdbc')}"

    testImplementation "org.junit.vintage:junit-vintage-engine:${project.property('junit.version')}"
}

//copy the instrumentation tests from the maven osmdroid-android-it src folder
compileJava {
    doFirst {
        copy {
            from("${rootDir}/osmdroid-android/src/main/java/org/osmdroid/tileprovider/util/StreamUtils.java")
            into "src/main/java/org/osmdroid/tileprovider/util/"
        }
        copy {
            from("${rootDir}/osmdroid-android/src/main/java/org/osmdroid/util/GEMFFile.java")
            into "src/main/java/org/osmdroid/util/"
        }
        copy {
            from("${rootDir}/osmdroid-android/src/main/java/org/osmdroid/api/") {
                exclude 'IMyLocationOverlay.java'
                exclude 'Polyline.java'
                exclude 'Marker.java'
                exclude 'IProjection.java'
            }
            into "src/main/java/org/osmdroid/api/"
        }
        delete {
            delete "${projectDir}/src/main/java/org/osmdroid/api/"
            delete "${projectDir}/src/main/java/org/osmdroid/api/IMyLocationOverlay.java"
            delete "${projectDir}/src/main/java/org/osmdroid/api/Polyline.java"
            delete "${projectDir}/src/main/java/org/osmdroid/api/Marker.java"
            delete "${projectDir}/src/main/java/org/osmdroid/api/IProjection.java"
        }
    }
}

afterEvaluate {
    publishing {

        publications {
            release(MavenPublication) {
                from components.java
                groupId project.property("pom.groupId")
                artifactId project.name
                version = project.property("pom.version")
                pom {
                    description =  project.description
                    url = project.property("pom.url")

                    //scm, organization and developers are injected via other mechanisms

                    licenses {
                        license {
                            name=project.property("pom.licenses.license.0.name");
                            url=project.property("pom.licenses.license.0.url");
                            distribution==project.property("pom.licenses.license.0.distribution");
                        }

                    }
                }
            }

            pdZipPublication(MavenPublication) {
                artifact "build/distributions/OSMMapTilePackager-" + project.property("pom.version") + ".zip"

            }
        }
    }

}
