description = 'OpenMap in the Play Store, the example app for using osmdroid'

apply plugin: 'com.android.application'
apply from: "$rootDir/gradle/android-signing.gradle"


group = project.property("pom.groupId")
version = project.property("pom.version")

android {
    namespace 'org.osmdroid'

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        applicationId "org.osmdroid"

        multiDexEnabled = true

        minSdkVersion 21

        compileSdk(project.hasProperty('android.compileSdkVersion')
                ? findProperty('android.compileSdkVersion').toInteger() : 23)

        // default targetSdkVersion to API 23, if not provided
        targetSdkVersion(project.hasProperty('android.targetSdkVersion')
                ? findProperty('android.targetSdkVersion').toInteger() : 23)

        // default versionCode o 1 if not provided
        versionCode(project.hasProperty('android.versionCode')
                ? Integer.parseInt(project.property('android.versionCode')) : 1)

        versionName project.version
    }
    lintOptions {
        abortOnError false
    }

    buildFeatures {
        buildConfig = true
    }

    testOptions {
        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    sourceSets {
        main {
            java {
                exclude '**/*_old.java'
                exclude '**/*_new.java'
                exclude '**/*_backup.java'
            }
        }
    }

    packagingOptions {
        pickFirst "androidsupportmultidexversion.txt"
        pickFirst "META-INF/AL2.0"
        pickFirst "META-INF/LGPL2.1"
    }

    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_17)
        targetCompatibility(JavaVersion.VERSION_17)
    }
}

dependencies {
    //apparently google or gradle changed something so that
    //transitive dependencies of submodules in the same project
    //are not longer resolved...

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") { because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib") }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") { because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib") }
    }

    //osmdroid-mapsforge
    implementation 'org.mapsforge:mapsforge-map-android:0.18.0'
    implementation 'org.mapsforge:mapsforge-map:0.18.0'
    implementation 'org.mapsforge:mapsforge-themes:0.13.0'

    implementation project(':osmdroid-android')
    implementation project(':osmdroid-geopackage')
    implementation project(':osmdroid-mapsforge')
    implementation project(':osmdroid-wms')
    implementation project(':osmdroid-shape')

    implementation 'io.github.missioncommand:mil-sym-android-renderer:0.1.48'

    //used for various demos for picking offline tile archives
    implementation 'com.github.angads25:filepicker:1.1.1'

    //used for bookmark demo
    implementation group: 'com.opencsv', name: 'opencsv', version: '4.4'

    //usual android stuff
    implementation "com.google.android.material:material:1.11.0" //needed for UI menuing
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.lifecycle:lifecycle-common:2.7.0"

    implementation "androidx.appcompat:appcompat:1.6.1"
    //crash logging
    implementation 'ch.acra:acra:4.7.0'

    //memory leak testing
    implementation 'com.squareup.leakcanary:leakcanary-android:2.2'

    //on device testing
    androidTestImplementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation "androidx.annotation:annotation:1.7.1"

    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test:rules:1.5.0"
    // Optional -- UI testing with Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    // Optional -- UI testing with UI Automator
    androidTestImplementation "androidx.test.uiautomator:uiautomator:2.2.0"
    // Optional -- UI testing with Compose
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.5.4"
}

//the following sets the required permissions for API 23+ devices and AVDs

android.applicationVariants.configureEach { variant ->
    if (variant.getBuildType().name == "debug") {
        tasks.register('configDevice${variant.name.capitalize()}', Exec) {
            dependsOn variant.installProvider.get()

            group = 'nameofyourtaskgroup'
            description = 'Describe your task here.'

            def adb = android.getAdbExecutable().toString()
            def mypermission = 'android.permission.ACCESS_FINE_LOCATION'
            commandLine "$adb shell pm grant ${variant.applicationId} $mypermission".split(' ')
        }
        variant.testVariant.connectedInstrumentTestProvider.configure {
            dependsOn "configDevice${variant.name.capitalize()}"
        }
    }
}

android.applicationVariants.configureEach { variant ->
    if (variant.getBuildType().name == "debug") {
        tasks.register('configDevice2${variant.name.capitalize()}', Exec) {
            dependsOn variant.installProvider.get()

            group = 'nameofyourtaskgroup'
            description = 'Describe your task here.'

            def adb = android.getAdbExecutable().toString()
            def mypermission = 'android.permission.WRITE_EXTERNAL_STORAGE '
            commandLine "$adb shell pm grant ${variant.applicationId} $mypermission".split(' ')
        }
        variant.testVariant.connectedInstrumentTestProvider.configure {
            dependsOn "configDevice2${variant.name.capitalize()}"
        }
    }
}
