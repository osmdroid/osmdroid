description = 'Simple osmdroid based map application'

apply plugin: 'com.android.application'
apply from: "$rootDir/gradle/android-signing.gradle"

group = project.property("pom.groupId")
version =  project.property("pom.version")

android {
     namespace 'org.osmdroid.simplemap'

     defaultConfig {

         applicationId project.group + "." +
                 project.name.toString().toLowerCase().replaceAll("[-]", ".")
                         .replaceAll("[^a-zA-Z0-9\\.]", "")

         multiDexEnabled = true
         minSdkVersion findProperty('android-minSdkForSupportLib.version').toInteger()

         compileSdk(project.hasProperty('android.compileSdkVersion')
                 ? findProperty('android.compileSdkVersion').toInteger() : 23)


         targetSdkVersion(project.hasProperty('android.targetSdkVersion')
                 ? findProperty('android.targetSdkVersion').toInteger() : 23)

         versionCode(project.hasProperty('android.versionCode')
                 ? Integer.parseInt(project.property('android.versionCode')) : 1)

         versionName project.version
     }

    lint {
        baseline = file("lint-baseline.xml")
    }

    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_17)
        targetCompatibility(JavaVersion.VERSION_17)
    }
}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation project(':osmdroid-android')
}



