buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        google()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.6.3"
    }
}

plugins {
    id 'checkstyle'
}

ext {
    compileSdk = 28
}

task checkstyle(type: Checkstyle) {
    description = 'Check Java style with Checkstyle'
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    source = './'
    include '**/*.java'
    exclude '**/build/generated/**'
    classpath = files()
    ignoreFailures = true
}

checkstyle {
    toolVersion = "8.15"
}

allprojects {
    // NOTE: project.group and project.version must be defined before including
    // maven-support.gradle since it uses these values...
    project.group = (project.hasProperty('pom.groupId')
            ? project.property('pom.groupId') : "")

    project.version =
            (project.hasProperty('pom.version') ? project.property('pom.version') : "1.0")

    // forces all changing dependencies (i.e. SNAPSHOTs) to automatically download
    // (thanks, @BillBarnhill!)
    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://repo.maven.apache.org/maven2" }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

}
