description = 'Geopackage support for osmdroid'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = project.property("pom.groupId")
version = project.property("pom.version")

android {
	namespace 'org.osmdroid.gpkg'

    defaultConfig {
        minSdkVersion 23
        compileSdk findProperty('android.compileSdkVersion').toInteger()
        targetSdkVersion  findProperty('android.targetSdkVersion').toInteger()
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    packagingOptions {
        resources {
            excludes += ['DebugProbesKt.bin', 'com/j256/ormlite/core/LICENSE.txt']
        }
    }


    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_17)
        targetCompatibility(JavaVersion.VERSION_17)
    }

}

dependencies {
    //crash logging
    //implementation 'ch.acra:acra:4.7.0'
    implementation("androidx.lifecycle:lifecycle-common:2.8.4")

    testImplementation "org.junit.vintage:junit-vintage-engine:${project.property('junit.version')}"

    api project(':osmdroid-android')
    api ('mil.nga.geopackage:geopackage-android:6.7.4'){
        exclude group: 'com.google.android.gms', module:'play-services'
        exclude group: 'com.google.maps.android', module:'android-maps-utils'
        exclude group: 'com.android.support',module: 'support-v13'
        exclude group: 'org.osmdroid.gpkg'
        exclude module: 'ormlite-core'
        exclude group: 'com.j256.ormlite'
    }

    api 'mil.nga.geopackage:geopackage-core:6.6.7'
    api 'ar.com.hjg:pngj:2.1.0'
    api 'mil.nga:tiff:3.0.0'

    api 'ar.com.hjg:pngj:2.1.0'
    api 'mil.nga:tiff:3.0.0'
}

tasks.register('androidSourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
}

artifacts {
//    archives packageReleaseJar
    archives androidSourcesJar

}

afterEvaluate {
    publishing {

        publications {
            release(MavenPublication) {
                from components.release
                groupId project.property("pom.groupId")
                artifactId project.name
                version = project.property("pom.version")
                pom {
                    description = project.description
                    url = project.property("pom.url")

                    //scm, organization and developers are injected via other mechanisms

                    licenses {
                        license {
                            name = project.property("pom.licenses.license.0.name");
                            url = project.property("pom.licenses.license.0.url");
                            distribution == project.property("pom.licenses.license.0.distribution");
                        }

                    }
                }
            }
        }
    }


}

//apply from: "$rootDir/gradle/javadoc-aar.gradle"
