description = "An Android library to display OpenStreetMap views using Mapsforge. May use non ASF licensed dependencies"
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = project.property("pom.groupId")
version =  project.property("pom.version")

android {
	namespace 'org.osmdroid.mapsforge'

    defaultConfig {
        minSdkVersion 21
        compileSdk findProperty('android.compileSdkVersion').toInteger()
        targetSdkVersion  findProperty('android.targetSdkVersion').toInteger()
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_17)
        targetCompatibility(JavaVersion.VERSION_17)
    }
}

dependencies {
    implementation("androidx.lifecycle:lifecycle-common:2.8.3")

    testImplementation "org.junit.vintage:junit-vintage-engine:${project.property('junit.version')}"

    //Mapsforge rendering and database support, which is LGPL
    api 'org.mapsforge:mapsforge-map-android:0.21.0'
    api 'org.mapsforge:mapsforge-map:0.21.0'
    api 'org.mapsforge:mapsforge-themes:0.21.0'

    //osmdroid which is ASF licensed
    api  project(':osmdroid-android' )
}

tasks.register('androidSourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
}

artifacts {
//    archives packageReleaseJar
    archives androidSourcesJar
}

afterEvaluate {
    publishing {

        publications {
            release(MavenPublication) {
                from components.release
                groupId project.property("pom.groupId")
                artifactId project.name
                version = project.property("pom.version")
                pom {
                    description =  project.description
                    url = project.property("pom.url")

                    //scm, organization and developers are injected via other mechanisms

                    licenses {
                        license {
                            name=project.property("pom.licenses.license.0.name");
                            url=project.property("pom.licenses.license.0.url");
                            distribution==project.property("pom.licenses.license.0.distribution");
                        }

                    }
                }
            }
        }
    }


}

//apply from: "$rootDir/gradle/javadoc-aar.gradle"
